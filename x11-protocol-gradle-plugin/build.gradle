plugins {
    id 'groovy-gradle-plugin'
    id 'maven-publish'
    id 'signing'
}

gradlePlugin {
    plugins {
        simplePlugin {
            id = 'com.github.moaxcp.x11protocol'
            implementationClass = 'com.github.moaxcp.x11protocol.X11ProtocolPlugin'
        }
    }
}

repositories {
    mavenCentral()
}

test {
    useJUnitPlatform()
}

dependencies {
    implementation 'org.eclipse.collections:eclipse-collections-api:11.1.0'
    implementation 'com.google.guava:guava:32.1.3-jre'
    implementation 'pl.allegro.finance:tradukisto:1.8.0'
    implementation 'org.projectlombok:lombok:1.18.30'
    implementation 'com.squareup:javapoet:1.13.0'

    testImplementation platform('org.spockframework:spock-bom:2.3-groovy-3.0')
    testImplementation 'net.bytebuddy:byte-buddy:1.14.10'
    testImplementation 'org.objenesis:objenesis:3.3'
    testImplementation 'org.spockframework:spock-core'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.1'
    testImplementation 'com.google.truth:truth:1.1.5'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            
            artifactId = project.name
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = project.name
                description = project.description
                url = 'https://github.com/moaxcp/x11'
                licenses {
                    license {
                        name = 'The MIT License (MIT)'
                        url = 'http://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'moaxcp'
                        name = 'John Mercier'
                        email = 'moaxcp@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/moaxcp/x11.git'
                    developerConnection = 'scm:git:git@github.com:moaxcp/x11.git'
                    url = 'https://github.com/moaxcp/x11'
                }
            }
            repositories {
                maven {
                    name = "OSS"
                    def releasesRepoUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
                    def snapshotsRepoUrl = 'https://oss.sonatype.org/content/repositories/snapshots'
                    url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                    credentials {
                        username = project.findProperty('nexus.username')
                        password = project.findProperty('nexus.password')
                    }
                }
                maven {
                    name = "GitHubPackages"
                    url = uri("https://maven.pkg.github.com/moaxcp/x11")
                    credentials {
                        username = project.findProperty('github.user')
                        password = project.findProperty('github.key')
                    }
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}