package com.github.moaxcp.x11protocol.xcbparser

import com.squareup.javapoet.ArrayTypeName
import com.squareup.javapoet.CodeBlock
import com.squareup.javapoet.TypeName
import groovy.transform.EqualsAndHashCode
import groovy.transform.ToString

@ToString(includePackage = false)
@EqualsAndHashCode
class JavaPad implements JavaUnit, JavaReadParameter {
    JavaClass javaClass
    XUnit x11Field
    int bytes
    boolean readParam
    TypeName readTypeName

    @Override
    String getName() {
        int index = javaClass.protocol.findIndexOf {it.is(this) }
        if(index) {
            return "pad$index"
        }
        return 'pad'
    }

    @Override
    XUnit getXUnit() { //todo why is this not generated by groovy
        return x11Field
    }

    @Override
    CodeBlock getDeclareCode() {
        return CodeBlock.builder().addStatement('$T $L = $L', getTypeName(), getName(), getDefaultValue()).build()
    }

    @Override
    TypeName getTypeName() {
        return ArrayTypeName.of(byte.class)
    }

    @Override
    TypeName getReadTypeName() {
        if(readTypeName) {
            return readTypeName
        }
        return typeName
    }

    @Override
    CodeBlock getDeclareAndReadCode() {
        return CodeBlock.builder().addStatement('$T $L = $L', getTypeName(), getName(), getReadCode()).build()
    }

    @Override
    CodeBlock getReadCode() {
        CodeBlock.of("in.readPad($bytes)")
    }

    @Override
    void addBuilderCode(CodeBlock.Builder code) {
        throw new IllegalStateException('JavaPad cannot be used with builder')
    }

    @Override
    void addWriteCode(CodeBlock.Builder code) {
        code.addStatement("out.writePad($bytes)")
    }

    @Override
    boolean isReadProtocol() {
        return !readParam
    }

    @Override
    CodeBlock getSizeExpression() {
        return CodeBlock.of('$L', bytes)
    }

    @Override
    Optional<Integer> getFixedSize() {
        return Optional.of(bytes)
    }

    @Override
    CodeBlock getDefaultValue() {
        CodeBlock.of('[]')
    }
}
