plugins {
    id 'java-library'
    id 'org.unbroken-dome.test-sets'
    id 'io.freefair.lombok'
    id 'maven-publish'
    id 'signing'
    id 'jacoco'
}

description = 'An x11 client implemented in java'

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
    withSourcesJar()
}

dependencies {
    api project(':x11-keysym')
    api project(':x11-protocol:x11-protocol-core')
    api project(':x11-protocol:x11-protocol-bigreq')
    api project(':x11-protocol:x11-protocol-composite')
    api project(':x11-protocol:x11-protocol-damage')
    api project(':x11-protocol:x11-protocol-dpms')
    api project(':x11-protocol:x11-protocol-dri2')
    api project(':x11-protocol:x11-protocol-dri3')
    api project(':x11-protocol:x11-protocol-ge')
    api project(':x11-protocol:x11-protocol-glx')
    api project(':x11-protocol:x11-protocol-present')
    api project(':x11-protocol:x11-protocol-randr')
    api project(':x11-protocol:x11-protocol-record')
    api project(':x11-protocol:x11-protocol-render')
    api project(':x11-protocol:x11-protocol-res')
    api project(':x11-protocol:x11-protocol-screensaver')
    api project(':x11-protocol:x11-protocol-shape')
    api project(':x11-protocol:x11-protocol-shm')
    api project(':x11-protocol:x11-protocol-sync')
    api project(':x11-protocol:x11-protocol-xcmisc')
    api project(':x11-protocol:x11-protocol-xevie')
    api project(':x11-protocol:x11-protocol-xf86dri')
    api project(':x11-protocol:x11-protocol-xf86vidmode')
    api project(':x11-protocol:x11-protocol-xfixes')
    api project(':x11-protocol:x11-protocol-xinerama')
    api project(':x11-protocol:x11-protocol-xinput')
    api project(':x11-protocol:x11-protocol-xkb')
    api project(':x11-protocol:x11-protocol-xprint')
    api project(':x11-protocol:x11-protocol-xproto')
    api project(':x11-protocol:x11-protocol-xselinux')
    api project(':x11-protocol:x11-protocol-xtest')
    api project(':x11-protocol:x11-protocol-xv')
    api project(':x11-protocol:x11-protocol-xvmc')

    testImplementation 'org.assertj:assertj-core:3.16.0'
    testImplementation 'org.mockito:mockito-core:3.3.3'
    testImplementation 'org.mockito:mockito-junit-jupiter:3.3.3'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.0'
}

test {
    useJUnitPlatform()
}

jacocoTestReport {
    reports {
        xml.required = true
    }
}

testSets {
    integrationTest
}

integrationTest {
    useJUnitPlatform()
    jacoco {
        enabled = false
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            
            artifactId = project.name
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = project.name
                description = project.description
                url = 'https://github.com/moaxcp/x11'
                licenses {
                    license {
                        name = 'The MIT License (MIT)'
                        url = 'http://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'moaxcp'
                        name = 'John Mercier'
                        email = 'moaxcp@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/moaxcp/x11.git'
                    developerConnection = 'scm:git:git@github.com:moaxcp/x11.git'
                    url = 'https://github.com/moaxcp/x11'
                }
            }
            repositories {
                maven {
                    name = "OSS"
                    def releasesRepoUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
                    def snapshotsRepoUrl = 'https://oss.sonatype.org/content/repositories/snapshots'
                    url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                    credentials {
                        username = project.findProperty('nexus.username')
                        password = project.findProperty('nexus.password')
                    }
                }
                maven {
                    name = "GitHubPackages"
                    url = uri("https://maven.pkg.github.com/moaxcp/x11")
                    credentials {
                        username = project.findProperty('github.user')
                        password = project.findProperty('github.key')
                    }
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}